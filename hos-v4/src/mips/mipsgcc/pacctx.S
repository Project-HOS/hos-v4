/* ------------------------------------------------------------------------ */
/*  Hyper Operating System V4  μITRON4.0仕様 Real-Time OS                  */
/*    プロセッサ抽象化コンポーネント (MIPS4互換コア用)                      */
/*                                                                          */
/*                                  Copyright (C) 1998-2008 by Project HOS  */
/*                                  http://sourceforge.jp/projects/hos/     */
/* ------------------------------------------------------------------------ */
#include "mipsregs.h"

		.global hospac_dis_int				/* 割り込み禁止					*/
		.global hospac_ena_int				/* 割り込み許可					*/
		.global hospac_cre_ctx_asm			/* 実行コンテキストの作成		*/
		.global hospac_swi_ctx				/* 実行コンテキストの切替		*/
/***********************************************************************
 *  割り込み禁止
 *  void hospac_dis_int(void)
 ***********************************************************************/
/* ステータス・レジスタのIEビットを0にする */
		.text
		.align	4
hospac_dis_int:
		mfc0	t0, CP0_STATUS			/* CP0レジスタをロード */
		nop
		la		t1, 0xF1FFFFF8			/* ERL=0,IE=0,EXL=0 */
		and		t0, t1, t0
		mtc0	t0, CP0_STATUS			/* CP0レジスタをストア */
		nop
		jr		ra
		nop
/***********************************************************************
 *  割り込み許可
 *  void hospac_ena_int(void)
 ***********************************************************************/
/* ステータス・レジスタのIEビットを1にする */
		.text
		.align	4
hospac_ena_int:
		mfc0	t0, CP0_STATUS			/* CP0レジスタをロード */
		nop
		la		t1, 0xF1FFFFFB
		and		t0, t1, t0				/* ERL=0 */
		la		t1, 0x00000001			/* IE=1 */
		or		t0, t1, t0
		mtc0	t0, CP0_STATUS			/* CP0レジスタをストア */
		nop
		jr		ra
		nop
/***********************************************************************
 *  実行コンテキストエントリーアドレス
 ***********************************************************************/
		.text
		.align	4
ctx_entry:
		j		a1						/* 実行アドレスa1にジャンプ。a0が実行時パラメータ */
		nop
/***********************************************************************
 *  実行コンテキストの作成
 *  void hospac_cre_ctx_asm(
 *  	 T_HOSPAC_CTXINF *pk_ctxinf,    作成するコンテキスト
 *  	 VP     sp,                     スタックポインタ
 *  	 void   (*task)(VP_INT),        実行アドレス
 *  	 VP_INT exinf)                  実行時パラメータ
 ***********************************************************************/
		.text
		.align	4
hospac_cre_ctx_asm:
		move	t8, a1                  /* スタックポインタを退避 */
		subu	a1, a1, 288             /* スタックの伸張（4*72=288バイト）*/
	//	sw		a2,  5*4(a1)            /* 実行アドレスの格納 */
		sw		a3,  4*4(a1)            /* 実行パラメータの格納 */
		sw		t8, 29*4(a1)            /* スタックポインタの設定 */
#if 0
		la		t8, ctx_entry           /* 実行エントリポイントをリターンアドレスに設定 */
		sw		t8, 31*4(a1)
#else
		sw		a2, 31*4(a1)
#endif
		mfc0	t8, CP0_STATUS          /* CP0_STATUSを退避 */
		nop
		sw		t8, 36*4(a1)
		nop
		sw		a1, (a0)                /* コンテキストのスタックポインタ保存 */
		jr		ra                      /* 呼び出し元へ帰る */
		nop
/***********************************************************************
 *  実行コンテキストの切替
 *  void hospac_swi_ctx(
 *  	T_HOSPAC_CTXINF *pk_pre_ctxinf, 現在のコンテキストの保存先
 *  	T_HOSPAC_CTXINF *pk_nxt_ctxinf) 切り替えるコンテキスト
 ***********************************************************************/
		.text
		.align	4
hospac_swi_ctx:
		pushall							/* レジスタ退避				*/
		sw		sp, (a0)				/* spを管理領域へ退避		*/
		lw		sp, (a1)				/* 管理領域からspを復帰		*/
		popall							/* レジスタ復帰				*/
		jr		ra						/* コンテキストへジャンプ	*/
		nop
/* ------------------------------------------------------------------------ */
/*  Copyright (C) 1998-2008 by Project HOS                                  */
/* ------------------------------------------------------------------------ */
