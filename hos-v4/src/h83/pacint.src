; --------------------------------------------------------------------------- 
;  HOS-V4                                                                     
;    割り込みハンドラ (日立 H8/300H 用)                                       
;                                                                             
;                                 Copyright (C) 1998-2002 by Ryuji Fuchikami  
; --------------------------------------------------------------------------- 


		.CPU     300HA

		.SECTION D, DATA, ALIGN=2
		.SECTION X, DATA, ALIGN=2
		.SECTION P, CODE, ALIGN=2

		.IMPORT  _kernel_sys_sp		; システム用スタックのアドレス



; -----------------------------------------------
;          割り込み処理
;
;  割り込み発生時は er0 を push した後、er0に
;  割り込み番号をセットした後、ここにジャンプする
;  ものとする。
; -----------------------------------------------
		.IMPORT	_kernel_sta_int		; 割り込み処理開始
		.IMPORT	_kernel_end_int		; 割り込み処理終了
		.IMPORT	_kernel_exe_int		; 割り込み処理実行
		.IMPORT	_kernel_int_sp		; 割り込み用スタックアドレス
		.IMPORT	_kernel_h83_imsk	; 割り込みマスク値
		.EXPORT	_HOS_int_hdr		; 割り込みハンドラ

_HOS_int_hdr:
	; ---- レジスタ保存
		push.l	er1
		
	; ----割り込みマスク設定
		stc.b	ccr, r1l
		mov.b	r1l, @_kernel_h83_imsk
		
	; -------- 多重割り込みの判別
		mov.w	@__HOS_int_cnt, r1
		bne	mul_int
		
	; ======== 最初の割り込み ========
	; ---- 割り込みネストカウンタ設定
		mov.w	#1, r1
		mov.w	r1, @__HOS_int_cnt
		
	; ---- スタックポインタの入れ替え
		mov.l	er7, @__HOS_int_sp
		mov.l	@_kernel_int_sp, er1
		mov.l	@er1, er7
		
	; ---- 割り込み処理の開始
		jsr	@_kernel_sta_int
		
	; -------- 割り込みハンドラの処理
		jsr	@_kernel_exe_int
		
	; -------- スタックポインタ復帰
		mov.l	@__HOS_int_sp, er7
		
	; -------- 割り込みネストカウンタクリア
		xor.w	r1, r1
		mov.w	r1, @__HOS_int_cnt	; 割り込みカウンタクリア
		
	; -------- タスクコンテキストに移行
		jsr	@_kernel_end_int
		
	; -------- 復帰
		pop.l	er1
		pop.l	er0
		rte


	; ======== 多重割り込み ========
mul_int:	
	; -------- 割り込みネストカウンタ加算
		inc.w	#1, r1
		mov.w	r1, @__HOS_int_cnt
		
	; -------- 割り込みハンドラ呼び出し
		jsr	@_kernel_exe_int
		
	; -------- 割り込みネストカウンタ減算
		mov.w	@__HOS_int_cnt, r0
		dec.w	#1, r0
		mov.w	r0, @__HOS_int_cnt
		
	; -------- 復帰
		pop.l	er1
		pop.l	er0
		rte



; -----------------------------------------------
;       割り込み処理データ
; -----------------------------------------------
		.SECTION  B, DATA, ALIGN=2

__HOS_int_sp:	.RES.L	1	; 割り込み時の er7 保存用
__HOS_int_cnt:	.RES.W	1	; 割り込みネストカウンタ


		.END



; --------------------------------------------------------------------------- 
;  Copyright (C) 1998-2002 by Ryuji Fuchikami                                 
; --------------------------------------------------------------------------- 
